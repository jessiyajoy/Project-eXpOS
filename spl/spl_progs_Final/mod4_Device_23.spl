// -------- DEVICE MANAGER: DISK_STORE(1), DISK_LOAD(2), TERMINAL_WRITE(3), TERMINAL_READ(4) -----------------

alias functionNum R1;
alias currentPID R2;

// ---- Disk Store (num = 1) ------
// Args - PID, Page Number, Block Number; Return NIL
// Called by Shutdown, Buffer Read and Buffer Write.

if( functionNum == 1 ) then
    alias pageNo R3;
    alias blockNo R4;

    //Call Acquire Disk function in resource manager module
    multipush(R2, R3, R4); 
    R1 = 3;
    R2 = currentPID;
    call MOD_0; 
    multipop(R2, R3, R4);

    //LOAD/STORE BIT, PAGE NUMBER and BLOCK NUMBER in the Disk Status Table
    [DISK_STATUS_TABLE + 1] = 1;
    [DISK_STATUS_TABLE + 2] = pageNo;
    [DISK_STATUS_TABLE + 3] = blockNo;

    store(pageNo, blockNo);

    [PROCESS_TABLE + ( currentPID * 16) + 4] = WAIT_DISK;

    //call module 5 SCHEDULER
    call SCHEDULER;

    return;
endif ;

// ---- Disk Load (num = 2) ------
if(functionNum == 2) then
    alias currentPID R2;
    alias pageNum R3;
    alias blockNum R4;

    // Call Acquire Disk function in the resource manager module (module 0)
    multipush(R1, R2, R3, R4);
    R1 = 3;
    R2 = currentPID;
    call MOD_0;
    multipop(R1, R2, R3, R4);

    // Disk Status table, LOAD, page, block
    [DISK_STATUS_TABLE + 1] = 0;
    [DISK_STATUS_TABLE + 2] = pageNum;
    [DISK_STATUS_TABLE + 3] = blockNum;
    
    load(pageNum, blockNum);
    [PROCESS_TABLE + currentPID*16 + 4] = WAIT_DISK;
    
    // Scheduler
    multipush(R1, R2, R3, R4);
    call SCHEDULER;
    multipop(R1, R2, R3, R4);
    return;
endif;


// ---- Terminal Write Function (num = 3) ----
if(functionNum == 3) then
    alias currentPID R2;
    // Acquire Terminal First
    multipush(R1, R2, R3);
    R1 = 8;
    R2 = currentPID;
    // Call Resource Manager
    call MOD_0;
    multipop(R1, R2, R3);


    // Print the word in R3
    print(R3);

    // Release Terminal Now
    multipush(R1, R2, R3);
    R1 = 9;
	R2 = currentPID;
    // Call Resource Manager 
	call MOD_0;
	multipop(R1, R2, R3);
	return;
endif;

// ---- Terminal Read Function (num = 4) ----
if(functionNum == 4) then
    alias currentPID R2;
    //Acquire Terminal
    multipush(R1, R2, R3);
    R1 = 8;
    R2 = currentPID;
    // Call Resource Manager
    call MOD_0;

	multipop(R1, R2, R3);
    // Read statement
    read;
    // set STATE to waiting for terminal to Read.
    [PROCESS_TABLE + currentPID * 16 + 4] = WAIT_TERMINAL;

    // MOD_5 for Context Switch (Scheduler)
    multipush(R1, R2, R3);
    call SCHEDULER;
    multipop(R1, R2, R3);

    // R3 has Logical address of the storage buffer, 
    //offset 8 of Process Table has Input Buffer Field

    alias physicalAddBuffer R4;
	physicalAddBuffer = [PTBR + ( 2 * R3 ) / 512] * 512 + R3 % 512;
	[physicalAddBuffer] = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE +1] * 16 + 8];

    return;
endif;
