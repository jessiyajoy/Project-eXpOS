// ----------------- FORK SYSTEM CALL ------------------------------

//Mode
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+1]*16 + 9] = 8;
alias userSP R5;
userSP = SP;

alias processEntryP R14;
processEntryP = PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16;

//--Switch Stack
//save user SP, set SP to KPTR
[processEntryP + 13] = SP;
SP = [processEntryP + 11]*512 - 1;

//GET_PCB_ENTRY, PROCESS MANAGER
multipush(R5, R14);
R1 = 1;
call MOD_1;
multipop(R5, R14);

//save PID of child
alias chPID R6; 
chPID = R0;

if( chPID == -1 ) then
    //---16 Processes already running
    //store -1 return value at userSP - 1
    alias physicalAddrRetVal R15;
    physicalAddrRetVal = ([PTBR + 2 * ((userSP-1)/512)] * 512) + ((userSP-1)%512);
    [physicalAddrRetVal] = -1;

    //switch stack, mode
    goto end;
endif ;

[processEntryP+14] = PTBR;
alias chPTBR R7;
chPTBR = PAGE_TABLE_BASE + chPID*20;
alias pPTBR R8;
pPTBR = PAGE_TABLE_BASE + [SYSTEM_STATUS_TABLE+1]*20;


//parent & child share same HEAP
//if heap pages were not allocated for the parent , allocate it now.
if( [pPTBR + 4] == -1 || [pPTBR+5] == "0000") then
    // GET_FREE_PAGE, MEMORY MANAGER
    multipush(R5, R6, R7, R8, R14);
    R1=1;
    call MOD_2;
    multipop(R5, R6, R7, R8, R14);
    [ pPTBR + 4]=R0;
    [ pPTBR + 5]="0110";

    multipush(R5, R6, R7, R8, R14);
    R1=1;
    call MOD_2;
    multipop(R5, R6, R7, R8, R14);
    [ pPTBR + 6]=R0;
    [ pPTBR + 7]="0110";
endif ;

//--Allocate 2 STACK AND 1 USER AREA PAGE
//GET_FREE_PAGE, MEMORY MANAGER
multipush(R5, R6, R7, R8, R14);
R1=1;
call MOD_2;
multipop(R5, R6, R7, R8, R14);
[chPTBR + 16] = R0;
[chPTBR + 17] = [pPTBR + 17];

multipush(R5, R6, R7, R8, R14);
R1=1;
call MOD_2;
multipop(R5, R6, R7, R8, R14);
[chPTBR + 18] = R0;
[chPTBR + 19] = [pPTBR + 19];

multipush(R5, R6, R7, R8, R14);
R1=1;
call MOD_2;
multipop(R5, R6, R7, R8, R14);
[PROCESS_TABLE + chPID*16 + 11] = R0;

alias processEntryC R13;
processEntryC = PROCESS_TABLE + chPID*16;

//PROCESS TABLE -- CHILD ----------------------------------------------

//Copy PARENT's Process Table Entry to child 
//USERID, SWAP FLAG, USER AREA SWAP STATUS, INODE INDEX, UPTR 
[processEntryC + 3] = [processEntryP + 3];
[processEntryC + 6] = [processEntryP + 6];
[processEntryC + 10] = [processEntryP + 10];
[processEntryC + 7] = [processEntryP + 7];
[processEntryC + 13] = [processEntryP + 13];

//TICK, MODE FLAG, KPTR = 0
[processEntryC + 0] = 0;
[processEntryC + 9] = 0;
[processEntryC + 12] = 0;


//PPID, STATE, USER AREA PAGE NUMBER
[processEntryC + 2] = [SYSTEM_STATUS_TABLE + 1];
[processEntryC + 4] = CREATED;
//[PROCESS_TABLE + chPID*16 + 11] = ua;

//PID, PTBR, PTLR already initialized Get Pcb Entry function
//no need to initialize INPUT BUFFER.


//PER-PROCESS RESOURCE TABLE -- CHILD -----------------------------------------------
alias cnt R9;
cnt = 0;
alias resourceTable R15;
resourceTable = [PROCESS_TABLE + chPID*16 + 11]*512 + 496;
while( cnt < 16 ) do
    [resourceTable + cnt] = [ [PROCESS_TABLE+[SYSTEM_STATUS_TABLE+1]*16+11] * 512 + 496 + cnt];

    //If resource is semaphore (check Resource Identifier field in per-process resource table)
    //using sempahore table index, 
    //increment PROCESS COUNT field in semaphore table entry
    if( cnt%2 == 0 &&  [resourceTable + cnt]==1 ) then
        [SEMAPHORE_TABLE + [resourceTable + cnt + 1]*4 + 1] = [SEMAPHORE_TABLE + [resourceTable + cnt + 1]*4 + 1] + 1;
    endif ;

    // If the resource is a file
    // using open file table index, increment OPEN INSTANCE COUNT field in open file table entry.
    if( cnt%2 == 0 &&  [resourceTable + cnt]==0 ) then
        [OPEN_FILE_TABLE + [resourceTable + cnt + 1]*4 + 1] = [OPEN_FILE_TABLE + [resourceTable + cnt + 1]*4 + 1] + 1;
    endif ;
    
    cnt = cnt + 1;
endwhile ;



//DISK_MAP_TABLE -- CHILD

alias diskMapTable R10;
diskMapTable = DISK_MAP_TABLE + 10*chPID;
cnt = 0;
while( cnt < 10 ) do
    [ diskMapTable + cnt ] = [DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE+1] * 10 + cnt];
    cnt = cnt + 1;
endwhile ;


//PAGE_TABLE -- CHILD

cnt = 0;
while( cnt < 8 ) do
    [chPTBR + cnt*2] = [pPTBR + cnt*2];
    [chPTBR + cnt*2 +1] = [pPTBR + cnt*2 +1];
    [MEMORY_FREE_LIST + [chPTBR + cnt*2]] = [MEMORY_FREE_LIST + [chPTBR + cnt*2]] + 1;
    cnt = cnt + 1;
endwhile ;


//Copy word by word the contents of the parent user stack to that of the child.
cnt = 0;
while( cnt < 512 ) do
    [[chPTBR + 16]*512 + cnt] = [[pPTBR + 16]*512 + cnt];
    [[chPTBR + 18]*512 + cnt] = [[pPTBR + 18]*512 + cnt];
    cnt = cnt + 1;
endwhile ;


//Store the value in the BP register on top of the kernel stack of child process. 
//This value will be used to initialize the BP register of the child process by the scheduler when the child is scheduled for the first time.
[[processEntryC + 11]*512] = BP;


//---Store return value at userSP - 1
//PARENT : Child PID
//CHILD : 0
alias physicalAddrRetVal R15;
physicalAddrRetVal = ([pPTBR + 2*((userSP-1)/512)] * 512) + ((userSP-1)%512);
[physicalAddrRetVal] = chPID;
physicalAddrRetVal = ([chPTBR + 2*((userSP-1)/512)] * 512) + ((userSP-1)%512);
[physicalAddrRetVal] = 0;
end:
//switch stack, mode
SP = userSP; 
[processEntryP + 9] = 0;
ireturn;
